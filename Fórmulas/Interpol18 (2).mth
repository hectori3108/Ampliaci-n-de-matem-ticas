"Fichero INTERPOL18.mth"

"Contiene las siguientes funciones:"

"LF(v,x) ==> Interpolador de LAGRANGE para datos procedentes de una funci¢n"

"                  donde v=[x0, x1, ..., xn]"

"HC(datoshc,x) ==> Interpolador a trozo c£bico de Hermite con datos num‚ricos~
,"

"                  donde datoshc=[ [x0,y0,d0], [x1,y1,d1], ..., [xn,yn,dn]]"

"SP(datossp,x) ==> Spline natural con datos num‚ricos, "

"                  donde datossp:= [[x0,y0], [x1,y1], ..., [xn,yn]]"

"HCF(v,x) ==> Interpolador a trozos c£bico de Hermite para datos de una funci~
¢n con v=[x0,x1,... xn]"

"SPF(v,x) ==> Spline Natural para datos de una funci¢n donde v=[x0,x1,...,xn]~
"

":::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::"

"Nota 1: En los interpoladores a trozos NO es necesario que LAS ABSCISAS DE L~
OS DATOS ESTN EN ORDEN CRECIENTE"

":::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::"

"* Fichero INTCUB.MTH ******************"

"********** C£bica de Hermite a trozos para datos num‚ricos *****************~
*"

"Polinomio de Hermite para el subintervalo i-‚simo de la partici¢n"

datos_:=

;Sust(Nueva)
PH_I(datos_,i_,x_):=datos_ SUB i_ SUB 2+(x_-datos_ SUB i_ SUB 1)*(datos_ SUB ~
i_ SUB 3+(x_-datos_ SUB i_ SUB 1)/(datos_ SUB (i_+1) SUB 1-datos_ SUB i_ SUB ~
1)^2*(datos_ SUB (i_+1) SUB 2-datos_ SUB i_ SUB 2-(datos_ SUB (i_+1) SUB 1-da~
tos_ SUB i_ SUB 1)*datos_ SUB i_ SUB 3+(x_-datos_ SUB (i_+1) SUB 1)/(datos_ S~
UB (i_+1) SUB 1-datos_ SUB i_ SUB 1)*((datos_ SUB (i_+1) SUB 3+datos_ SUB i_ ~
SUB 3)*(datos_ SUB (i_+1) SUB 1-datos_ SUB i_ SUB 1)-2*(datos_ SUB (i_+1) SUB~
 2-datos_ SUB i_ SUB 2))))

HC_ord(datos_,x_):=SUM(CHI(datos_ SUB i_ SUB 1,x_,datos_ SUB (i_+1) SUB 1)*PH~
_I(datos_,i_,x_),i_,1,DIMENSION(datos_)-1)

HC(datos_,x_):=HC_ord(SORT(datos_),x_)

"********** Fin HC ***"

"********** Splines para datos num‚ricos ******************"

"===> Funci¢n de espaciado"

;Sust(Nueva)
HI(v_):=VECTOR(v_ SUB i_-v_ SUB (i_-1),i_,2,DIMENSION(v_))

"===> Construcci¢n de la matriz del sistema."

;Sust(Nueva)
DIAG(h_):=APPEND([1],VECTOR(2*(h_ SUB i_+h_ SUB (i_+1)),i_,1,DIMENSION(h_)-1)~
,[1])

;Sust(Nueva)
UDIAG(h_):=APPEND([0],VECTOR(h_ SUB (i_+1),i_,1,DIMENSION(h_)-1),[0])

;Sust(Nueva)
LDIAG(h_):=APPEND([0],VECTOR(h_ SUB i_,i_,1,DIMENSION(h_)-1),[0])

BOS(i_,j_,l1_,u1_):=IF(i_>j_,l1_ SUB i_,u1_ SUB i_)

MA0(i_,j_,d1_,l1_,u1_):=IF(ABS(i_-j_)=1,BOS(i_,j_,l1_,u1_),IF(i_=j_,d1_ SUB i~
_,0))

MAF(n_,d1_,l1_,u1_):=VECTOR(VECTOR(MA0(i_,j_,d1_,l1_,u1_),j_,1,n_),i_,1,n_)

"===> T‚rmino independiente"

TINDEP(v_,h_):=APPEND(APPEND([0],VECTOR(3*((v_ SUB (i_+2)-v_ SUB (i_+1))/h_ S~
UB (i_+1)+(v_ SUB i_-v_ SUB (i_+1))/h_ SUB i_),i_,1,DIMENSION(v_)-2)),[0])

"===> Resoluci¢n de sistema (coeficientes ci)"

;Sust(Nueva)
COC(m_):=ROW_REDUCE(MAF(DIMENSION(m_),DIAG(HI(m_` SUB 1)),LDIAG(HI(m_` SUB 1)~
),UDIAG(HI(m_` SUB 1))),TINDEP(m_` SUB 2,HI(m_` SUB 1)))` SUB (DIMENSION(m_)+~
1)

"===> Resto de los coeficientes"

;Sust(Nueva)
COB(v_,h_,c_):=VECTOR((v_ SUB (i_+2)-v_ SUB (i_+1))/h_ SUB (i_+1)-h_ SUB (i_+~
1)/3*(2*c_ SUB (i_+1)+c_ SUB (i_+2)),i_,0,DIMENSION(v_)-2)

;Sust(Nueva)
COD(v_,h_,c_):=VECTOR(1/(3*h_ SUB (i_+1))*(c_ SUB (i_+2)-c_ SUB (i_+1)),i_,0,~
DIMENSION(v_)-2)

"Matriz de coeficientes (por columnas c,b,d)"

;Sust(Nueva)
CO(m_,c_):=APPEND([VECTOR(c_ SUB i_,i_,1,DIMENSION(m_)-1)],APPEND([COB(m_` SU~
B 2,HI(m_` SUB 1),c_)],[COD(m_` SUB 2,HI(m_` SUB 1),c_)]))`

"===> Formaci¢n de los polinomios c£bicos para cada subintervalo"

SI13(v_,w_,x_):=v_ SUB 2+(x_-v_ SUB 1)*(w_ SUB 2+(x_-v_ SUB 1)*(w_ SUB 1+(x_-~
v_ SUB 1)*w_ SUB 3))

"(Se usar  en la forma SI13(dai,mcoi,x)"

SPAUX(da_,mco_,x_):=SUM(CHI(da_ SUB i_ SUB 1,x_,da_ SUB (i_+1) SUB 1)*SI13(da~
_ SUB i_,mco_ SUB i_,x_),i_,1,DIMENSION(da_)-1)

SP_ord(da_,x_):=SPAUX(da_,CO(da_,COC(da_)),x_)

SP(da_,x_):=SP_ord(SORT(da_),x_)

"********** Fin Spline ***"

":::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::"

" "

":::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::"

"Interpoladores de LAGRANGE, HERMITE CÜBICO a trozos y SPLINE para datos proc~
edentes de una funci¢n "

f(x):=

LF(v_,x_):=POLY_INTERPOLATE(TABLE(f(s_),s_,v_),x_)

;Sust(Nueva)
HCF(v_,x_):=HC(TABLE([f(s_),f'(s_)],s_,v_),x_)

SPF(v_,x_):=SP(TABLE(f(s_),s_,v_),x_)

":::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::"

" "

